%{
#include <stdio.h>
#include <stdlib.h>
#include "absyn.h"
#include "symbol.h"

void yyerror(E_Pos *locp, const char *s);
%}

%code {
    int yylex(YYSTYPE*, YYLTYPE*);
}

%union {
    int ival;
    char *sval;
    A_Exp exp;
    A_Var var;
    A_Dec dec;
    A_Type type;
    A_ExpList explist;
    A_DecList declist;
    A_EField efield;
    A_EFieldList efieldlist;
    A_Field field;
    A_FieldList fieldlist;
    A_FunDec fundec;
    A_FunDecList fundeclist;
}

%define api.location.type {E_Pos}
%define parse.error detailed 
%define api.pure full
%locations

%token <sval> ID
%token <ival> INT
%token PLUS MINUS TIMES DIVIDE EQ NEQ LT LE GT GE
%token ASSIGN
%token ARRAY IF THEN ELSE WHILE FOR TO DO LET IN END OF 
%token BREAK NIL
%token FUNCTION VAR TYPE
%token LPAREN RPAREN COMMA COLON SEMICOLON LBRACE RBRACE

%type <exp> program exp int_exp call_exp record_exp seq_exp assign_exp if_exp while_exp for_exp break_exp let_exp
%type <var> var_exp var simple_var
%type <explist> args exp_list exp_seq
%type <field> field
%type <fieldlist> field_list field_list_nonempty
%type <efield> efield
%type <efieldlist> efield_list efield_list_nonempty
%type <declist> dec_list dec_list_nonempty
%type <dec> dec var_dec func_dec type_dec
%type <type> ty


%left PLUS MINUS
%left TIMES DIVIDE
%nonassoc EQ NEQ LT LE GT GE
%right ASSIGN
%nonassoc LOW_PRECEDENCE
%nonassoc LOWER_THAN_ELSE
%nonassoc ELSE

%start program

%%

program: exp { $$ = $1; };

exp: var_exp { $$ = make_A_VarExp(@1, $1); }
    | int_exp { $$ = $1; }
    | call_exp { $$ = $1; }
    | exp PLUS exp   { $$ = make_A_OpExp(@1, A_PLUS_OP, $1, $3); }
    | exp MINUS exp  { $$ = make_A_OpExp(@1, A_MINUS_OP, $1, $3); }
    | exp TIMES exp  { $$ = make_A_OpExp(@1, A_TIMES_OP, $1, $3); }
    | exp DIVIDE exp { $$ = make_A_OpExp(@1, A_DIVIDE_OP, $1, $3); }
    | record_exp { $$ = $1; }
    | seq_exp { $$ = $1; }
    | assign_exp { $$ = $1; }
    | if_exp { $$ = $1; }
    | while_exp { $$ = $1; }
    | for_exp { $$ = $1; }
    | break_exp { $$ = $1; }
    | let_exp { $$ = $1; };

var_exp: var { $$ = $1; }

var: simple_var { $$ = $1; };

simple_var: ID { $$ = make_A_SimpleVar(@1, make_S_Symbol($1)); };

int_exp: INT { $$ = make_A_IntExp(@1, $1); };

call_exp: ID LPAREN args RPAREN { $$ = make_A_CallExp(@1, make_S_Symbol($1), NULL); };

args: /* empty */ { $$ = NULL; }
    | exp_list { $$ = $1; }
    ;

exp_list: exp { $$ = make_A_ExpList($1, NULL); }
    | exp_list COMMA exp { $$ = make_A_ExpList($3, $1); }
    ;

exp_seq: exp { $$ = make_A_ExpList($1, NULL); }
    | exp_seq SEMICOLON exp { $$ = make_A_ExpList($3, $1); }
    ;

record_exp: ID LBRACE efield_list RBRACE
          { $$ = make_A_RecordExp(@1, make_S_Symbol($1), $3); };

field_list: /* empty */ { $$ = NULL; }
          | field_list_nonempty { $$ = $1; }
          ;

field_list_nonempty: field { $$ = make_A_FieldList($1, NULL); }
                   | field_list_nonempty COMMA field { $$ = make_A_FieldList($3, $1); }
                   ;

field: ID COLON ID { $$ = make_A_Field(@1, make_S_Symbol($1), make_S_Symbol($3)); };

efield_list: /* empty */ { $$ = NULL; }
           | efield_list_nonempty { $$ = $1; }
           ;

efield_list_nonempty: efield { $$ = make_A_EFieldList($1, NULL); }
                    | efield_list_nonempty COMMA efield { $$ = make_A_EFieldList($3, $1); }
                    ;

efield: ID EQ exp { $$ = make_A_EField(make_S_Symbol($1), $3); };


seq_exp: LPAREN exp_seq RPAREN { $$ = make_A_SeqExp(@1, $2); }
    ;

assign_exp: var ASSIGN exp { $$ = make_A_AssignExp(@1, $1, $3); }
    ;

if_exp: IF exp THEN exp %prec LOWER_THAN_ELSE { $$ = make_A_IfExp(@1, $2, $4, NULL); }
    | IF exp THEN exp ELSE exp { $$ = make_A_IfExp(@1, $2, $4, $6); }
    ;

while_exp: WHILE exp DO exp %prec LOW_PRECEDENCE { $$ = make_A_WhileExp(@1, $2, $4); }
    ;

for_exp: FOR ID ASSIGN exp TO exp DO exp %prec LOW_PRECEDENCE { $$ = make_A_ForExp(@1, make_S_Symbol($2), $4, $6, $8); }
    ;

break_exp: BREAK { $$ = make_A_BreakExp(@1); }
    ;

let_exp: LET dec_list IN exp_list END { $$ = make_A_LetExp(@1, $2, make_A_SeqExp(@4, $4)); }
    ;

dec_list: /* empty */ { $$ = NULL; }
    | dec_list_nonempty { $$ = $1; }
    ;

dec_list_nonempty: dec { $$ = make_A_DecList($1, NULL); }
    | dec_list_nonempty dec { $$ = make_A_DecList($2, $1); }
    ;

dec: type_dec { $$ = $1; }
    | var_dec { $$ = $1; }
    | func_dec { $$ = $1; }
    ;

type_dec: TYPE ID EQ ty { $$ = make_A_TypeDecGroup(@1, make_A_TypeDecList(make_A_TypeDec(make_S_Symbol($2), $4), NULL)); };


ty: ID { $$ = make_A_NameType(@1, make_S_Symbol($1)); }
    | LBRACE field_list RBRACE { $$ = make_A_RecordType(@1, $2); }
    | ARRAY OF ID { $$ = make_A_ArrayType(@1, make_S_Symbol($3)); }
    ;

var_dec: VAR ID ASSIGN exp { $$ = make_A_VarDec(@1, make_S_Symbol($2), NULL, $4); }
    | VAR ID COLON ID ASSIGN exp { $$ = make_A_VarDec(@1, make_S_Symbol($2), make_S_Symbol($4), $6); }
    ;

func_dec: FUNCTION ID LPAREN field_list RPAREN EQ exp {
    $$ = make_A_FunctionDecGroup(@1, make_A_FunDecList(make_A_FunDec(@1, make_S_Symbol($2), $4, NULL, $7), NULL));
}
| FUNCTION ID LPAREN field_list RPAREN COLON ID EQ exp {
    $$ = make_A_FunctionDecGroup(@1, make_A_FunDecList(make_A_FunDec(@1, make_S_Symbol($2), $4, make_S_Symbol($7), $9), NULL));
};

%% 
 void yyerror(YYLTYPE *locp, const char *s) {
    fprintf(stderr, "Error: %s\n", s);
    fprintf(stderr, "Location: %d:%d - %d:%d\n", locp->first_line, locp->first_column, locp->last_line, locp->last_column);
}